# Maintainer: Andrew Krasichkov <dev@buglloc.cc>

pkgname=packetti
pkgver=1.0.0
pkgrel=1
pkgdesc="The extcap application to integrate Packetti with Wireshark or something"
arch=("x86_64")
url="https://github.com/buglloc/packetti/software"
license=("MIT")
depends=("glibc")
makedepends=(
  "git"
  "go"
)
options=("!lto")
source=("git+https://github.com/buglloc/packetti.git#bc1139bcf85997ceba4d18d2c31638d6b00d9ba9")
sha256sums=('9cc2c36fba118f30f56484b57f974829a5562256b2aefd98d4ae243239aa3be1')

prepare() {
  cd "$pkgname/software"
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "$pkgname/software"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  go build -v ./cmd/packetticap
}

package() {
  cd "$pkgname/software"
  install -vDdm755 -t "$pkgdir/usr/lib/wireshark/extcap" packetticap
}
