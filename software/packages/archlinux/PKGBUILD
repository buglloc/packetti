# Maintainer: Andrew Krasichkov <dev@buglloc.cc>

pkgname=packetti
pkgver=0.0.1
pkgrel=1
pkgdesc="The extcap application to integrate Packetti with Wireshark or something"
arch=("x86_64")
url="https://github.com/buglloc/packetti/software"
license=("MIT")
depends=("glibc")
makedepends=(
  "go"
)
options=("!lto")
source=("https://github.com/buglloc/packetti/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('7e6c696c2ad0435f42800ac0dd5197c0a9035bf27a49cdfcd6fff2b1a50b678a')

prepare() {
  cd "$pkgname-$pkgver/software"
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "$pkgname-$pkgver/software"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  go build -v ./cmd/packetticap
}

package() {
  cd "$pkgname-$pkgver/software"
  install -vDm755 -t "$pkgdir/usr/lib/wireshark/extcap" packetticap
}
